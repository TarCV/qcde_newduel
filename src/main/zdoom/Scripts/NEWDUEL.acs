#library "NEWDUEL"
#include "zcommon.acs"

#libdefine MAXPLAYERS 64
#libdefine MAXMATES 5
#libdefine NUMCHAMPS 20
#libdefine TICSPERSECOND 35
#libdefine USE_KEY "+qcduel_use"

world int 88:chosenClass[];
world int 87:duelPlayers[];

world int 86:roundActive;
#libdefine ROUND_INIT 0
#libdefine ROUND_PREDRAFT 1
#libdefine ROUND_DRAFTING 2
#libdefine ROUND_WARMUP 3
#libdefine ROUND_STARTED 4
#libdefine ROUND_ILLEGAL_STATE -1


int champStatus[MAXMATES][2];
#libdefine STATUS_UNUSED 0
#libdefine STATUS_USED 1
#libdefine STATUS_DEAD 2

#libdefine AFTER_DRAFTING_PAUSE 3

#libdefine ITEM_ACTIVATED "QCDActivatedPlayer"
#libdefine ITEM_ACTIVATED_TOKEN "QCDActivatedPlayerToken"
#libdefine ITEM_DEACTIVATED "QCDNeutralizePlayer"
#libdefine ITEM_DEACTIVATED_EFFECT "QCDNeutralizePlayerEffect"
#libdefine ITEM_FROZEN "QCDFrozenPlayer"
#libdefine ITEM_NOTFROZEN "QCDUnfrozenPlayer"
#libdefine ITEM_NOTFROZEN_TOKEN "QCDUnfrozenPlayerToken"

int champDraftingTime = 10;
bool pressedUse[2];

str champions[NUMCHAMPS] = {
	"Ranger",
	"DoomSlayer",
	"Corvus",
	"Bitterman",
	"Galen",
	"Duke Nukem",
	"Sarge",
	"Grayson",
	"Kane",
	"Caleb",
	"Menelkir",
	"Nyx",
	"Durandal",
	"Lo Wang",
	"Terminator",
	"Zedek",
	"Inquisitor",
	"Sorgaul",
	"Keel",
	"Eradicator"
};
str championFaces[NUMCHAMPS] = {
	"PORTRA01",
	"PORTRA02",
	"PORTRA03",
	"PORTRA10",
	"PORTRA11",
	"PORTRA14",
	"PORTRA17",
	"PORTRA19",
	"PORTRA04",
	"PORTRA05",
	"PORTRA06",
	"PORTRA12",
	"PORTRA15",
	"PORTRA18",
	"PORTRA07",
	"PORTRA08",
	"PORTRA09",
	"PORTRA13",
	"PORTRA16",
	"PORTRA20"
};
str championSprites[NUMCHAMPS] = {
	"QP01A",
	"QP02A",
	"QP03A",
	"QP10A",
	"QP11A",
	"QP14E",
	"QP17A",
	"QP19A",
	"QP04A",
	"QP05A",
	"QP06A",
	"QP12A",
	"QP15A",
	"QP18A",
	"QP07A",
	"QP08A",
	"QP09A",
	"QP13A",
	"QP16A",
	"QP20A"
};
str champ_actions[MAXMATES] = {"+moveleft", "+forward", "+moveright", "+back", "+jump"};
int champ_buttons[MAXMATES] = {BT_MOVELEFT, BT_FORWARD, BT_MOVERIGHT, BT_BACK, BT_JUMP};

#libdefine ON_OPEN 0
#libdefine ON_PLAYERS_PRESENT 1
#libdefine ON_PLAYERS_DRAFTED 2
#libdefine ON_ROUND_STARTED 3
#libdefine ON_ROUND_FINISHED 4
#libdefine ON_ROUND_ABORTED 5


bool draftReady[2];
int whoDraftsNow = -1;
int whoDraftsFirst = 0;

Script "QCDE_Duel_Event"(int type, int arg1, int arg2) Event {
	if (type == 5) { // GAMEEVENT_ROUND_STARTS
		updateMatchStatus(ON_ROUND_STARTED);
	} else if (type == 6) { // ended or aborted
		updateMatchStatus(ON_ROUND_FINISHED);
	} else if (type == 7) {
		updateMatchStatus(ON_ROUND_ABORTED);
	} else if (type == 0) { // Activator fragged 'arg1' player
	}
}

Script "QCDE_Duel_Use" (/**TEST_TYPE:bool**/int down) Net {
	pressedUse[duelIndex()] = down;
}

Script "QCDED_World_Loop" (void) {
	printBold(s:"World loop started");
	int lastRound = -1;
	while (true) {
		if (lastRound != roundActive) {
			lastRound = roundActive;
			switch (roundActive) {
				case ROUND_PREDRAFT:
					ACS_NamedExecuteWait("QCDED_WaitForTwoPlayers", 0);
					whoDraftsFirst = random(0, 1);
					whoDraftsNow = playerNumberForDuelIndex(whoDraftsFirst);
					updateMatchStatus(ON_PLAYERS_PRESENT);
					break;
				case ROUND_DRAFTING:
					ACS_NamedExecuteWait("QCDED_WaitForDraftingCompletion", 0);
					killSafely(1000 + playerNumberForDuelIndex(0));
					killSafely(1000 + playerNumberForDuelIndex(1));
					updateMatchStatus(ON_PLAYERS_DRAFTED);
					break;
                case ROUND_ILLEGAL_STATE:
                    printBold(s:"New Duel system crashed. Please send demo to the author");
                    break;
			}
		}
		delay(17);
	}
}

function int playerNumberForDuelIndex(int di) {
    if (di > 1) return -1000;
    return duelPlayers[di] - 1;
}

Script "QCDED_Player_Loop" Enter {
	int lastRound = -1;
	bool activated;
	int pn = playerNumber();
	while (playerIsSpectator(pn) == 0) {
		if (lastRound != roundActive) {
			lastRound = roundActive;
			switch (roundActive) {
				case ROUND_DRAFTING:
					activated = false;
					ACS_NamedExecuteAlways("QCDE_Duel_ShowDraft", 0);
					ACS_NamedExecuteAlways("QCDE_Duel_Draft", 0);
					break;
			}
		}

		if (checkInventory(ITEM_ACTIVATED_TOKEN) < 1) {
			neutralizeMe();
		}
		if (checkInventory(ITEM_NOTFROZEN_TOKEN) < 1) {
			SetPlayerProperty(0, ON, PROP_TOTALLYFROZEN);
		}
		delay(7);
	}
	printBold(s:"Player loop finished");
}

Script "QCDE_Duel_Open" Open {
	updateMatchStatus(ON_OPEN);
	ACS_NamedExecute("QCDED_World_Loop", 0);
}

Script "QCDED_WaitForTwoPlayers" (void) {
	while (true) {
        int pc = playerCount();
        if (pc >= 2) {
            int cursor = 0;
            for (int i = 0; i < pc && cursor < 2; ++i) {
                if (playerIsSpectator(i) == 0) {
                    duelPlayers[cursor] = i + 1;
                    draftReady[cursor] = false;
                    ++cursor;
                }
            }
            if (cursor >= 2) {
                break;
            }
        }
        delay(17);
    }
}

Script "QCDED_WaitForDraftingCompletion" (void) {
	while (!draftReady[0] && !draftReady[1]) {
		delay(17);
	}
	delay(AFTER_DRAFTING_PAUSE*TICSPERSECOND); // Give players a chance to see the final selection
}

Script "QCDE_Duel_ShowDraft" (void) {
	int pn = playerNumber();
	if (playerIsBot(pn)) terminate;
	int yStep = (480 - 300) / MAXMATES;
	int xStep = yStep * 2;
	int di = duelIndex();
	int otherDi = 1 - di;
	while (roundActive == ROUND_DRAFTING) {
		for (int i = 0; i < MAXMATES; ++i) {
			int myChampion = getPickedClass(di, i);
			int othersChampions = getPickedClass(otherDi, i);

			setHudSize(640, 480, false);

			setFont("SMALLFONT");
			hudmessage(i:i+1;
				HUDMSG_PLAIN, 0, 0, 300.6, 220.1 + 16.0*i, 7.0 / TICSPERSECOND + 1);
			hudmessage(i:i+1;
				HUDMSG_PLAIN, 0, 0, 340.5, 220.1 + 16.0*i, 7.0 / TICSPERSECOND + 1);
			hudmessage(s:idToClass(myChampion);
				HUDMSG_PLAIN, 0, 0, 280.6, 220.1 + 16.0*i, 7.0 / TICSPERSECOND + 1);
			hudmessage(s:idToClass(othersChampions);
				HUDMSG_PLAIN, 0, 0, 360.5, 220.1 + 16.0*i, 7.0 / TICSPERSECOND + 1);

			if (myChampion >= 0) {
				setFont(championSprites[myChampion]);
				hudmessage(s:"A";
					HUDMSG_PLAIN, 0, 0, 75.0 + xStep*(i%2)*1.0, 170.2 + yStep*i*1.0, 7.0 / TICSPERSECOND + 1);
			} else if (myChampion < -1) {
				setFont("BIGFONT");
				hudmessage(i:-myChampion;
					HUDMSG_PLAIN, 0, 0, 75.0 + xStep*(i%2)*1.0, 170.2 + yStep*i*1.0, 7.0 / TICSPERSECOND + 1);
			} 
			if (othersChampions >= 0) {
				setFont(championSprites[othersChampions]);
				hudmessage(s:"A";
					HUDMSG_PLAIN, 0, 0, 640.0 - 75.0 - xStep*(i%2)*1.0, 170.2 + yStep*i*1.0, 7.0 / TICSPERSECOND + 1);
			} else if (othersChampions < -1) {
				setFont("BIGFONT");
				hudmessage(i:othersChampions;
					HUDMSG_PLAIN, 0, 0, 640.0 - 75.0 - xStep*(i%2)*1.0, 170.2 + yStep*i*1.0, 7.0 / TICSPERSECOND + 1);
			}
		}
		setHudSize(800, 600, false);
		int selectStep = 800 / NUMCHAMPS;
		for (int j = 0; j < NUMCHAMPS; ++j) {
			setFont(championSprites[j]);
			if (findChosen(j) < 0) {
				hudmessage(s:"A";
					HUDMSG_PLAIN, 0, 0, 0.1 + selectStep*j*1.0 - 20.0, 600.2 - 100.0, 7.0 / TICSPERSECOND + 1);
			}
		}

		delay(7);
	}
}

Script "QCDE_Duel_Draft" (void) {
	int forRound = 0;
	int di = duelIndex();
	int pn = playerNumber();
	resetPlayer(di);

	while (!draftReady[di]) {
		if (whoDraftsNow == pn) {
			if (playerIsBot(pn)) {
				setPickedClass(di, forRound, getPlayerInfo(pn, PLAYERINFO_PLAYERCLASS));
				// TODO: random class when round > 0
			} else {
				int draftingEnd = timer() + champDraftingTime*TICSPERSECOND;
				int selectedChamp = 0;
				while (findChosen(selectedChamp) != -1) {
					selectedChamp += 1;
				}
				while (true) {
					int buttons = getPlayerInput(pn, INPUT_BUTTONS);
					int dir = 0;
					int timeLeft = draftingEnd - timer();
					if (buttons == BT_MOVELEFT || buttons == BT_LEFT) {
						dir = -1;
					} else if (buttons == BT_MOVERIGHT || buttons == BT_RIGHT) {
						dir = 1;
					} else if (timeLeft <= 0 || pressedUse[pn]) {
						pressedUse[di] = false;
						setPickedClass(di, forRound, selectedChamp);
						if (forRound == 0) {
							setNextSpawnClass(selectedChamp);
						}
						break;
					}
					int timeLeftSeconds = timeLeft / TICSPERSECOND;
					if (timeLeftSeconds > 0) {
						setPickedClass(di, forRound, -1 - timeLeftSeconds);
					}

					if (dir != 0) {
						selectedChamp += dir;
						while (findChosen(selectedChamp) != -1) {
							selectedChamp += dir;
						}
					}

					while (selectedChamp < 0) {
						selectedChamp += NUMCHAMPS;
					}
					selectedChamp %= NUMCHAMPS;

					hudmessage(s:"Select - ", s:champions[selectedChamp], s:"?",
						s:" (",  i:timeLeft/TICSPERSECOND, s:" seconds left)";
						HUDMSG_PLAIN, 0, 0, 0.1, 0.9, 7.0 / TICSPERSECOND + 1);
					delay(3);
				}
			}
			if ((whoDraftsFirst + di + forRound) % 2 == 0) {
				whoDraftsNow = otherPlayer();
			}
			++forRound;
			if (forRound >= MAXMATES) draftReady[di] = true;
		}
		delay(17);
	}
}

Script "QCDE_Duel_Enter" Enter {
	ACS_NamedExecuteAlways("QCDE_Duel_Respawn", 0);
}
Script "QCDE_Duel_Respawn" Respawn {
	if (roundActive != ROUND_STARTED && roundActive != ROUND_WARMUP) terminate;

	int pn = playerNumber();
	int class = playerClass(pn);
	int chosenIndex = findChosen(class);
	int di = duelIndex();
	int i;
	bool isBot = playerIsBot(pn);
	if (isBot || getChampStatus(di, chosenIndex) == STATUS_UNUSED) {
		if (!isBot) {
			setChampStatus(di, chosenIndex, STATUS_USED);
			for (i = 0; i < MAXMATES; ++i) {
				if (getChampStatus(di, i) == STATUS_UNUSED) {
					setNextSpawnClass(getPickedClass(di, i));
					break;
				}
			}
		} else {
			for (i = 0; i < MAXMATES; ++i) {
				if (getPickedClass(di, i) >= 0 && getChampStatus(di, i) == STATUS_UNUSED) {
					setChampStatus(di, i, STATUS_USED);
					break;
				}
			}
		}
		SetPlayerProperty(0, OFF, PROP_TOTALLYFROZEN);
		giveInventory(ITEM_ACTIVATED, 1);
	} else {
		giveInventory(ITEM_DEACTIVATED, 1);

		neutralizeMe();
		delay(7);
		neutralizeMe();

		int mytid = activatorTID();
		if (mytid == 0) {
			mytid = 1000 + pn;
			while (isTidUsed(mytid)) {
				++mytid;
			}
			Thing_ChangeTID(0, mytid);
			printBold(s:"My tid - ", d:mytid);
		}

		if (playerHealth() > 0 && mytid != 0) {
			killSafely(mytid);
		}
		print(s:"Select a champion and press ", k:USE_KEY, s:" to spawn");
	}
}

function void updateMatchStatus (int when) {
	printBold(s:"updateMatchStatus started.");
	printBold(s:"state was ", i:roundActive, s:", event was ", i:when); 

	if (roundActive == ROUND_INIT && when == ON_OPEN) {
		int warmupTime = getCVar("sv_duelcountdowntime");
		int minWarmup = MAXMATES * 10 + AFTER_DRAFTING_PAUSE;
		if (warmupTime < minWarmup) {
			printBold(s:"Server is configured incorrectly. Ask the administrator to set warmup to at least ", i:minWarmup, s:" seconds");
		}
		champDraftingTime = (warmupTime - AFTER_DRAFTING_PAUSE) / MAXMATES;
		if (champDraftingTime < 5) champDraftingTime = 5;

		roundActive = ROUND_PREDRAFT;
	} else if (roundActive == ROUND_PREDRAFT && when == ON_PLAYERS_PRESENT) {
		roundActive = ROUND_DRAFTING;
	} else if (roundActive == ROUND_DRAFTING && when == ON_PLAYERS_DRAFTED) {
		roundActive = ROUND_WARMUP;
	} else if (roundActive <= ROUND_STARTED
		&& (when == ON_ROUND_STARTED || when == ON_OPEN)) {
		// TODO: make sure both players have same number of champions
		roundActive = ROUND_STARTED;
	} else if (when == ON_ROUND_FINISHED || when == ON_ROUND_ABORTED) {
		roundActive = ROUND_INIT;
	} else if (roundActive == ROUND_STARTED && when == ON_OPEN) {
		// Intentionally empty. OPEN scripts are executed on round start
	} else if (roundActive == ROUND_ILLEGAL_STATE) {
        // Intentionally empty
    }else {
        printBold(s:"New Duel system crashed. Please send demo to the author. (Tech data: state=",
            i:roundActive, s: ", event=", i:when, s:")");
		roundActive = ROUND_ILLEGAL_STATE;
	}

	printBold(s:"state became ", i:roundActive, s:", event was ", i:when); 
	printBold(s:"updateMatchStatus ended");
}

function void killSafely(int playerTid) {
	int newtid = uniqueTID();
	spawnForced("PlayerReintroducer", -30000.0, -30000.0, 0, newtid, 0);
	setActivator(newtid);
	thing_Damage2(playerTid, 9999, "PlayerRemoved");
	setActivator(playerTid);
}

Script "QCDE_Duel_Death" Death
{
	if (roundActive != ROUND_STARTED && roundActive != ROUND_WARMUP) terminate;

	setHudSize(320, 240, false);
	bool canSpawn = false;
	int champIndex = -1;
	int di = duelIndex();
	int pn = playerNumber();
	while (getActorProperty(0, APROP_HEALTH) <= 0) {
		int nextClass = getNextSpawnClass();
        int i;
		for (i = 0; i < MAXMATES && !canSpawn; ++i) {
			canSpawn = (nextClass == getPickedClass(di, i) && getChampStatus(di, i) == STATUS_UNUSED);
		}
		if (canSpawn) {
			if (checkInventory(ITEM_FROZEN) > 0) {
				giveInventory(ITEM_NOTFROZEN, 1);
			}
		} else {
			if (checkInventory(ITEM_NOTFROZEN) > 0) {
				giveInventory(ITEM_FROZEN, 1);
				neutralizeMe();
			}
		}

		int index = 0;
		int buttons = getPlayerInput(pn, INPUT_BUTTONS);
		for (i = 0; i < MAXMATES; ++i) {
			if (getChampStatus(di, i) != STATUS_UNUSED) continue;

			int iterClass = getPickedClass(di, i);

			if (buttons == champ_buttons[i]) {
				champIndex = i;
			}

			str selected = "";
			if (i == champIndex) {
				setNextSpawnClass(iterClass);
				nextClass = getNextSpawnClass();
			}
			if (nextClass == iterClass) {
				selected = ">";
			}

			hudmessage(s:selected, s:"[", k:champ_actions[index], s:"] - ", s:champions[iterClass];
					HUDMSG_PLAIN, 0, 0, 60.4, 30.1+10.0*index, 7.0 / TICSPERSECOND + 1);
			++index;
		}
		delay(7);
	}
}

Script "QCDE_Duel_Disconnect" (int gone) Disconnect {
	int di = duelIndexFor(gone);
	resetPlayer(di);
	duelPlayers[di] = 0;
}

function void neutralizeMe(void) {
	SetPlayerProperty(0, ON, PROP_TOTALLYFROZEN);
	if (playerHealth() > 0) {
		if (checkInventory(ITEM_DEACTIVATED_EFFECT) == 0) {
			giveInventory(ITEM_DEACTIVATED_EFFECT, 1);
		}
		setActorVelocity(0, 0, 0, 0, FALSE, FALSE);
	}
}

function int getNextSpawnClass(void) {
	str selected = getCVarString("qcd_champ");
	for (int i = 0; i < NUMCHAMPS; ++i) {
		if (strcmp(selected, champions[i]) == 0) return i;
	}
	return -1;
}
function void setNextSpawnClass(int class) {
	setCVarString("qcd_champ", idToClass(class));
	consoleCommand("playerclass $qcd_champ");
}

function int findChosen(int class) {
	return findChosenFor(duelIndex(), class);
}
function int findChosenFor(int di, int class) {
	if (class < 0) return -100;
	for (int i = 0; i < MAXMATES; ++i) {
		if (class == getPickedClass(duelIndex(), i)) return i;
	}
	return -1;
}

function str idToClass(int id) {
	if (id < 0) {
		return "-";
	}
	return champions[id];
}

function int duelIndex(void) {
	return duelIndexFor(playerNumber());
}
function int duelIndexFor(int pn) {
	if (pn == playerNumberForDuelIndex(0)) {
		return 0;
	} else if (pn == playerNumberForDuelIndex(1)) {
		return 1;
	}
	return -1000; // set to a stupid value to catch bugs
}

function int otherPlayer(void) {
	return playerNumberForDuelIndex(1 - duelIndex());
}

function int getPickedClass(int duelPlayer, int forRound) {
	if (forRound < 0 || forRound >= MAXMATES) {
		return -100;
	}
	if (duelPlayer < 0 || duelPlayer >= 2) {
		return -100;
	}
	return chosenClass[MAXMATES*duelPlayer + forRound];
}

function void setPickedClass(int duelPlayer, int forRound, int newClass) {
	if (forRound < 0 || forRound >= MAXMATES) {
		return;
	}
	if (duelPlayer < 0 || duelPlayer >= 2) {
		return;
	}
	chosenClass[MAXMATES*duelPlayer + forRound] = newClass;
}

function int getChampStatus(int duelPlayer, int forRound) {
	if (forRound < 0 || forRound >= MAXMATES) {
		return -100;
	}
	if (duelPlayer < 0 || duelPlayer >= 2) {
		return -100;
	}
	return champStatus[forRound][duelPlayer];
}
function void setChampStatus(int duelPlayer, int forRound, int newStatus) {
	if (forRound < 0 || forRound >= MAXMATES) {
		return;
	}
	if (duelPlayer < 0 || duelPlayer >= 2) {
		return;
	}
	champStatus[forRound][duelPlayer] = newStatus;
}

function void resetPlayer(int duelPlayer) {
	for (int i = 0; i < MAXMATES; ++i) {
		setPickedClass(duelPlayer, i, -1);
		setChampStatus(duelPlayer, i, STATUS_UNUSED);
	}
}

/**TEST_ONLY_SCRIPTS
    @Override
    protected void consoleCommand(String command) {
        Thing activator = activatorInternal();
        if (activator instanceof PlayerPawn && command.equals("playerclass $qcd_champ")) {
            String newClass = getCVarString("qcd_champ");
            for (int i = 0; i < champions.length; i++) {
                if (champions[i].equalsIgnoreCase(newClass)) {
                    setCVarAsConsole("playerclass", i);
                    return;
                }
            }
            throw new IllegalArgumentException("Class " + newClass + " was not found");
        } else {
            super.consoleCommand(command);
        }
    }
**/